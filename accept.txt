*** Settings ***
Library  proc.py

*** Test Cases ***
Full Game No Strikes No Spares
    When I bowl "(9 0 9 0 9 0 9 0 9 0 9 0 9 0 9 0 9 0 9 0)"
    Then I should score a 90

Full Game No Strikes No Spares, Alternate Score
    When I bowl "(4 3 4 3 4 3 4 3 4 3 4 3 4 3 4 3 4 3 4 3)"
    Then I should score a 70

Full Game No Strikes, Spares
    When I bowl "(4 6 5 5 9 0 4 6 5 5 9 0 1 1 1 1 1 1 1 1)"
    Then I should score a 94

Full Game Strikes, No Doubles, No Spares
    When I bowl "(10 5 4 10 3 4 10 1 2 10 9 0 9 0 8 1)"
    Then I should score a 114

Full Game Strikes, Multi Strikes, Spares
    When I bowl "(10 9 1 8 1 7 1 10 10 10 9 1 8 2 9 0)"
    Then I should score a 180

Full Game Irregular 10th Frame, Spare
    When I bowl "(10 9 1 8 1 7 1 10 10 10 9 1 8 2 9 1 2)"
    Then I should score a 183

Perfect Game
    When I bowl "(10 10 10 10 10 10 10 10 10 10 10 10)"
    Then I should score a 300

Partial Game, No Strikes/Spares in Most Current Throw
    When I bowl "(5 4 3 4)"
    Then I should score a 16

Partial Game, No Strikes, Spare at the End
    When I bowl "(5 5 4 6)"
    Then I should score a 24

Incremental Game
    When I incrementally bowl "10 9 1 8 1 7 1 10 10 10 9 1 8 2 9 1 2 ^C"
    Then I should score "20\n38\n47\n55\n85\n114\n134\n152\n171\n183"

*** Keywords ***
I bowl "${struct_as_string}"     ${bowled}=           bowl           ${struct_as_string}
                                 set suite variable   ${bowled}
I incrementally bowl "${items}"  ${bowled}=           inc_bowl       ${items}
                                 set suite variable   ${bowled}
I should score a ${score:\d+}    should be equal      ${bowled}      ${score}
I should score "${items}"        should be equal      ${bowled}      ${items}
